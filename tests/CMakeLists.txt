cmake_minimum_required(VERSION 2.8)

project(ESL_tests CXX)

set(BUILD_GTEST ON CACHE BOOL "")
set(BUILD_GMOCK OFF CACHE BOOL "")
set(INSTALL_GTEST OFF CACHE BOOL "")
set(gtest_force_shared_crt ON CACHE BOOL "")
add_subdirectory(googletest)

function(esl_has_cxx feat)
	if(DEFINED "esl_has_cxx_${feat}")
		return()
	endif()
	list(FIND CMAKE_CXX_COMPILE_FEATURES ${feat} has_feat)
	if(${has_feat} GREATER_EQUAL 0)
		set("esl_has_cxx_${feat}" TRUE PARENT_SCOPE)
	else()
		set("esl_has_cxx_${feat}" FALSE PARENT_SCOPE)
	endif()
endfunction()

function(esl_add_target target name)
    add_executable(${target} ${name}.cpp)
    target_link_libraries(${target} esl gtest gtest_main)
	target_compile_options(${target} PRIVATE
				$<$<CXX_COMPILER_ID:MSVC>:/W3 /WX>
				$<$<CXX_COMPILER_ID:GNU>:-Wall -W -Werror -Wpedantic>
				$<$<CXX_COMPILER_ID:Clang>:-Wall -W -Werror -Wpedantic>
			)
	set_property(TARGET ${target} PROPERTY CXX_EXTENSIONS OFF)
	add_test(NAME ${target} COMMAND ${target})
endfunction()
	
function(esl_add_test name)
	esl_add_target(${name} ${name})
	foreach(feat ${ARGN})
		esl_has_cxx(${feat})
		if(NOT ${esl_has_cxx_${feat}})
			continue()
		endif()
		esl_add_target("${name}-${feat}" ${name})
		target_compile_features("${name}-${feat}" PRIVATE ${feat})
	endforeach()
endfunction()

esl_add_test(utils_tests)
esl_add_test(type_traits_tests)
esl_add_test(source_location_tests)
esl_add_test(functional_tests)
esl_add_test(span_tests)
esl_add_test(string_tests)
esl_add_test(endian_tests)
esl_add_test(map_utils_tests)
esl_add_test(linked_list_tests)
esl_add_test(lazy_tests)
esl_add_test(codecs_tests)
esl_add_test(unicode_tests)
esl_add_test(demangle_tests)

esl_add_test(shared_library_tests)
target_link_libraries(shared_library_tests ${CMAKE_DL_LIBS})
add_library(shared_library_tests_dl SHARED shared_library_tests_dl.cpp)
target_link_libraries(shared_library_tests_dl esl)
target_compile_definitions(shared_library_tests PRIVATE "ESL_TESTS_DL_FILENAME=$<TARGET_FILE:shared_library_tests_dl>")
add_dependencies(shared_library_tests shared_library_tests_dl)

